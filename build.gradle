plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '2.0.0'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.2.0'
}

group = 'io.github.ititus'
version = '1.0.0'

wrapper {
    gradleVersion = '8.13'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

modularity {
    disableEffectiveArgumentsAdjustment()
}

application {
    mainModule = 'io.github.ititus.skat'
    mainClass = 'io.github.ititus.skat.SkatClient'

    applicationDefaultJvmArgs = ['-Djava.util.logging.config.file=logging.properties']
}

repositories {
    mavenCentral()
}

configurations.configureEach {
    // the optional dependencies on jboss-marshalling / protobuf break in testing
    exclude group: 'io.netty', module: 'netty-codec-marshalling' // would require jboss marshalling
    exclude group: 'io.netty', module: 'netty-codec-protobuf' // would require google protobuf and javanano
}

dependencies {
    implementation 'io.github.ititus:ititus-commons:2.3.3'
    implementation 'it.unimi.dsi:fastutil:8.5.18'
    implementation platform('io.netty:netty-bom:4.2.9.Final')
    implementation 'io.netty:netty-buffer'
    implementation 'io.netty:netty-codec'
    implementation 'io.netty:netty-common'
    implementation 'io.netty:netty-handler'
    implementation 'io.netty:netty-resolver'
    implementation 'io.netty:netty-transport'

    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'org.mockito:mockito-core:5.21.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

javafx {
    version = '23.0.2'
    modules = ['javafx.base', 'javafx.controls', 'javafx.graphics']
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'skat'
    }
}
