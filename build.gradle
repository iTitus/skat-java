plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.7.0'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.23.3'
}

group = 'io.github.ititus'
version = '1.0.0'

wrapper {
    gradleVersion '6.8.1'
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_15
}

modularity {
    disableEffectiveArgumentsAdjustment()
}

application {
    mainModule = 'io.github.ititus.skat'
    mainClass = 'io.github.ititus.skat.SkatClient'

    applicationDefaultJvmArgs = ['-Djava.util.logging.config.file=logging.properties']
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'io.github.ititus:ititus-commons:1.4.0'
    implementation 'it.unimi.dsi:fastutil:8.5.2'
    implementation 'io.netty:netty-all:4.1.58.Final'

    testImplementation platform('org.junit:junit-bom:5.7.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.mockito:mockito-core:3.7.7'
}

compileTestJava {
    moduleOptions {
        addModules = ['org.junit.jupiter.params']
        addReads = ['io.github.ititus.skat': 'org.junit.jupiter.params']
    }
}

test {
    useJUnitPlatform()

    moduleOptions {
        addReads = ['io.github.ititus.skat': 'org.junit.jupiter.params']
        addExports = [
                'org.junit.platform.commons/org.junit.platform.commons.util'   : 'ALL-UNNAMED',
                'org.junit.platform.commons/org.junit.platform.commons.logging': 'ALL-UNNAMED'
        ]
        addOpens = ['io.github.ititus.skat/io.github.ititus.skat.network.buffer': 'org.junit.platform.commons']
    }
}

javafx {
    version = '15.0.1'
    modules = ['javafx.base', 'javafx.controls', 'javafx.graphics']
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages', '--compress', '2']
    launcher {
        name = 'skat'
    }
}
