plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
    id 'org.openjfx.javafxplugin' version '0.0.11'
    id 'org.beryx.jlink' version '2.24.4'
}

group = 'io.github.ititus'
version = '1.0.0'

wrapper {
    gradleVersion '7.4'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

modularity {
    disableEffectiveArgumentsAdjustment()
}

application {
    mainModule = 'io.github.ititus.skat'
    mainClass = 'io.github.ititus.skat.SkatClient'

    applicationDefaultJvmArgs = ['-Djava.util.logging.config.file=logging.properties']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.ititus:ititus-commons:2.0.1'
    implementation 'it.unimi.dsi:fastutil:8.5.6'
    implementation platform('io.netty:netty-bom:4.1.74.Final')
    implementation 'io.netty:netty-buffer'
    implementation 'io.netty:netty-codec'
    implementation 'io.netty:netty-common'
    implementation 'io.netty:netty-transport'

    testImplementation platform('org.junit:junit-bom:5.8.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.mockito:mockito-core:4.3.1'
}

test {
    useJUnitPlatform()
}

javafx {
    version = '17.0.2'
    modules = ['javafx.base', 'javafx.controls', 'javafx.graphics']
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages', '--compress', '2']
    launcher {
        name = 'skat'
    }
    mergedModule {
        excludeProvides servicePattern: 'reactor.blockhound.*'
    }
}
