plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.2'
}

group = 'io.github.ititus'
version = '1.0.0'

wrapper {
    gradleVersion '7.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

modularity {
    disableEffectiveArgumentsAdjustment()
}

application {
    mainModule = 'io.github.ititus.skat'
    mainClass = 'io.github.ititus.skat.SkatClient'

    applicationDefaultJvmArgs = ['-Djava.util.logging.config.file=logging.properties']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.ititus:ititus-commons:1.5.11'
    implementation 'it.unimi.dsi:fastutil:8.5.6'
    implementation platform('io.netty:netty-bom:4.1.69.Final')
    implementation 'io.netty:netty-buffer'
    implementation 'io.netty:netty-codec'
    implementation 'io.netty:netty-common'
    implementation 'io.netty:netty-transport'

    testImplementation platform('org.junit:junit-bom:5.8.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'org.mockito:mockito-core:4.0.0'
}

test {
    useJUnitPlatform()
}

javafx {
    version = '17.0.0.1'
    modules = ['javafx.base', 'javafx.controls', 'javafx.graphics']
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages', '--compress', '2']
    launcher {
        name = 'skat'
    }
    mergedModule {
        excludeProvides servicePattern: 'reactor.blockhound.*'
    }
}
